import foundation.e.blisslauncher.buildsrc.Libs
import foundation.e.blisslauncher.buildsrc.Versions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

// Manifest version information!
def versionMajor = 1
def versionMinor = 2
def versionPatch = 4

android {
    compileSdkVersion Versions.compile_sdk
    defaultConfig {
        applicationId "foundation.e.blisslauncher"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name == "debug") {
                variant.outputs.all { output ->
                    outputFileName = "BlissLauncher-${variant.getFlavorName()}-${variant.versionName}.apk"
                }
            }

        }
    }

    signingConfigs {
        debug {
            storeFile file(getRootDir().path + "/keystore/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    flavorDimensions "api"
    productFlavors {
        apiNougat {
            dimension "api"
        }
        apiOreo {
            dimension "api"
            minSdkVersion 26
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:1.0.0'

    apiNougatImplementation 'org.cyanogenmod:platform.sdk:6.0'
    apiOreoImplementation files('libs/lineage-sdk.jar')

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    implementation 'org.greenrobot:eventbus:3.1.1'

    // Support Libs
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.recyclerview

    // Rx Java and Android
    implementation Libs.RxJava.rxAndroid
    implementation Libs.RxJava.rxJava
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'

    // Retrofit
    implementation Libs.Retrofit.retrofit
    implementation Libs.Retrofit.gsonConverter
    implementation Libs.Retrofit.retrofit_rxjava_adapter

    // Okhttp
    implementation Libs.OkHttp.okhttp
    implementation Libs.OkHttp.loggingInterceptor

    // Rx Relay
    implementation "com.jakewharton.rxrelay2:rxrelay:${rootProject.ext.rxRelayVersion}"

    // Room
    implementation Libs.AndroidX.Room.runtime
    annotationProcessor Libs.AndroidX.Room.compiler

    // Unit test dependencies
    testImplementation "junit:junit:${rootProject.ext.junitVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.ext.robolectricVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-core:${rootProject.ext.hamcrestVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${rootProject.ext.hamcrestVersion}"
    testImplementation "org.hamcrest:hamcrest-integration:${rootProject.ext.hamcrestVersion}"

    // Instrumentation tests dependencies
    androidTestImplementation "junit:junit:${rootProject.ext.junitVersion}"
    androidTestImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
    androidTestImplementation "org.mockito:mockito-android:${rootProject.ext.mockitoVersion}"
    androidTestImplementation "com.android.support:support-annotations:${rootProject.ext.supportLibraryVersion}"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:${rootProject.ext.espressoVersion}", {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
    })
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.ext.espressoVersion}"
    androidTestImplementation "com.android.support.test:runner:${rootProject.ext.runnerRulesVersion}"
    androidTestImplementation "com.android.support.test:rules:${rootProject.ext.runnerRulesVersion}"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
}

apply plugin: 'com.getkeepsafe.dexcount'
